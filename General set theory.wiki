{{header
 | title    = General set theory
 | subtitle = 
 | left     = 
 | right    = 
 | shortcut = 
 | notes    = Here we start with the axioms of set theory and prove theorems for the usual concepts: empty set, singletons, unordered pairs, ordered pairs, relations, functions, some kinds of unions, intersection, set difference, and so on.  Although general set theory does not have any axioms which allow us to construct infinite sets, it does not forbid them either and all of the results here will be applicable for infinite sets as well, if supplemented by axioms which posit the existence of such sets. }}
{{interfaces
| imports = [[Interface:Axioms of general set theory]]
| exports = [[Interface:General set theory theorems]]
}}

We start by importing the theorems of [[Interface:Classical propositional calculus|propositional logic]] and [[Interface:First-order logic with quantifiability|predicate logic]], and the [[Interface:Axioms of general set theory]].  We also define some variables.
<jh>
import (CLASSICAL Interface:Classical_propositional_calculus () ())
import (FIRSTORDER Interface:First-order_logic_with_quantifiability (CLASSICAL) ())
import (AXIOMS Interface:Axioms_of_general_set_theory (CLASSICAL FIRSTORDER) ())

var (variable x y z w result)
var (object s t u A B C D A0 A1 B0 B1)
</jh>

{{under construction}}

== Empty set ==
The [[w:empty set|empty set]], or null set, is the set with no elements. We start with the conventional form of the [[w:Axiom of empty set|axiom of empty set]], which is a theorem rather than an axiom for us (and for many other axiomizations of set theory).<ref>[http://us.metamath.org/mpeuni/axnul.html axnul], metamath's set.mm, accessed July 19, 2010</ref>
<jh>
thm (axnul ((x y z)) () (∃ x (∀ y (¬ ((value y) ∈ (value x))))) (
</jh>
The only set theory axiom we use in the proof is the axiom of separation, which says that given the existence of any set (which is implicit in some the predicate logic steps), we can construct a subset of it which has no elements. Here is our application of separation, which we will use at the end of the proof (after a bunch of logical steps).
<jh>
        x y z (((value y) ∈ (value y)) ∧ (¬ ((value y) ∈ (value y)))) Separation
</jh>
The proof starts with <code>¬ (y ∈ y ∧ ¬ y ∈ y)</code>
<jh>
        (¬ ((value y) ∈ (value y))) TertiumNonDatur
        collectNegationNDN
</jh>
We now add <code>y ∈ z</code> to get <code>¬ (y ∈ z ∧ (y ∈ y ∧ ¬ y ∈ y))</code>
<jh>
        ((value y) ∈ (value z)) (((value y) ∈ (value y)) ∧ (¬ ((value y) ∈ (value y)))) ConjunctionLeftElimination
        introduceTransposition
        applyModusPonens
</jh>
We note that <code>y ∈ x ↔ (y ∈ z ∧ (y ∈ y ∧ ¬ y ∈ y))</code> implies itself.
<jh>
        (((value y) ∈ (value x)) ↔ (((value y) ∈ (value z)) ∧ (((value y) ∈ (value y)) ∧ (¬ ((value y) ∈ (value y)))))) ImplicationReflexivity
</jh>
We detach half the biconditional to get <code>(y ∈ x ↔ (y ∈ z ∧ (y ∈ y ∧ ¬ y ∈ y))) → ¬ y ∈ x</code>
<jh>
        negatedDetachImplicationBiconditionalRight
</jh>
We're almost there. We just need to add the quantifiers and apply the instance of the axiom of separation that we started with.
<jh>
        y addForAll
        x addThereExists
        applyModusPonens
))
</jh>

Now we introduce the symbol <code>∅</code> for the empty set.

<jh>
var (variable emptyElement)
def ((∅) (abstract emptyElement (⊥)))

thm (EmptySetVariable ((result x y)) () ((value x) ∉ (abstract y (⊥))) (
</jh>
To apply <code>Abstraction</code>, we need <code>∃ result ∀ x (x ∈ result ↔ [x / y]⊥)</code>. This follows from <code>axnul</code>, the <code>NegationBiconditional</code> relationship between negation and <code>⊥</code>, and the fact that substituting a variable in <code>⊥</code> has no effect.
<jh>
        result x axnul

        ((value x) ∈ (value result)) NegationBiconditional
        ((value x) ∈ (value result)) BiconditionalReflexivity
        (value x) y (⊥) NullSubstitution swapBiconditional
        buildBiconditional
        applyBiconditionalTransitivity

        x buildForAll
        result buildThereExists

        eliminateBiconditionalReverse
        applyModusPonens
</jh>
Now that we have <code>∃ result ∀ x (x ∈ result ↔ [x / y]⊥)</code>, we can apply <code>Abstraction</code> to get <code>x ∈ {y | ⊥} ↔ [x / y]⊥</code>.
<jh>
        Abstraction
</jh>
Moving the substitution which has no effect
<jh>
        (value x) y (⊥) NullSubstitution
        applyBiconditionalTransitivity
</jh>
gives us <code>x ∈ {y | ⊥} ↔ ⊥</code>
and then <code>NegationBiconditional</code> gives us our desired result.
<jh>
        ((value x) ∈ (abstract y (⊥))) NegationBiconditional
        eliminateBiconditionalForward
        applyModusPonens
))
</jh>

We provide a version with object instead of variable.
<jh>
thm (EmptySetObject ((x s y)) () (s ∉ (abstract y (⊥))) (
</jh>
The substitution to apply <code>VariableToObject</code> is <code>x = s → x ∉ ∅ ↔ s ∉ ∅</code>
<jh>
        (value x) s (abstract y (⊥)) IsElementBuilderRR
        addNegationInConsequent

        x y EmptySetVariable

        VariableToObject
))

thm (EmptySet ((s emptyElement)) () (s ∉ (∅)) (
        s emptyElement EmptySetObject
))
</jh>

== Adjunction for objects ==
The <code>Adjunction</code> axiom allows us to form a set with the elements of a given set and a single additional given element, where the given sets are represented by variables. Here we prove the same thing, but with object in place of variable. The proof is by straightforward application of <code>VariableToObject</code> with the usual substitution machinery.
<jh>
thm (AdjunctionObject-1 ((s x z w) (y x z w)) () (∃ w (∀ z (((value z) ∈ (value w)) ↔ (((value z) ∈ s) ∨ ((value z) = (value y)))))) (
        (value x) s (value z) IsElementBuilderLL
        ((value z) = (value y)) buildDisjunctionRRInConsequent
        ((value z) ∈ (value w)) buildBiconditionalLLInConsequent
        z buildForAllInConsequent
        w buildThereExistsInConsequent

        w z x y Adjunction

        VariableToObject
))

thm (AdjunctionObject ((s y z w) (t y z w)) () (∃ w (∀ z (((value z) ∈ (value w)) ↔ (((value z) ∈ s) ∨ ((value z) = t))))) (
        (value y) t (value z) EqualityBuilderLL
        ((value z) ∈ s) buildDisjunctionLLInConsequent
        ((value z) ∈ (value w)) buildBiconditionalLLInConsequent
        z buildForAllInConsequent
        w buildThereExistsInConsequent

        w z s y AdjunctionObject-1

        VariableToObject
))
</jh>

== Object version of extensionality ==
Here is a variant of extensionality, <code>∀ z (z ∈ A ↔ z ∈ B) → A = B</code>, which changes variables to objects. We first prove a lemma.

<jh>
thm (ExtensionalityObject-1 ((z A) (x y z) (x A)) () ((∀ z (((value z) ∈ A) ↔ ((value z) ∈ (value y)))) → (A = (value y))) (
</jh>
This lemma changes <code>x</code> to <code>A</code>, using <code>VariableToObject</code> applied to <code>x = A → ((∀ z (z ∈ x ↔ z ∈ y) → x = y) ↔ (∀ z (z ∈ A ↔ z ∈ y) → A = y))</code>
<jh>
        (value x) A (value z) IsElementBuilderLL
        ((value z) ∈ (value y)) buildBiconditionalRRInConsequent
        z buildForAllInConsequent

        (value x) A (value y) EqualityBuilderRR

        buildImplicationInConsequent

        z x y Extensionality
        VariableToObject
))

thm (ExtensionalityObject ((A z) (B z) (z y) (A y) (B y)) () ((∀ z (((value z) ∈ A) ↔ ((value z) ∈ B))) → (A = B)) (
</jh>
To prove this, we change <code>y</code> to <code>B</code>, using <code>VariableToObject</code> applied to <code>y = B → ((∀ z (z ∈ A ↔ z ∈ y) → A = y) ↔ (∀ z (z ∈ A ↔ z ∈ B) → A = B))
<jh>
        (value y) B (value z) IsElementBuilderLL
        ((value z) ∈ A) buildBiconditionalLLInConsequent
        z buildForAllInConsequent

        (value y) B A EqualityBuilderLL

        buildImplicationInConsequent

        z A y ExtensionalityObject-1
        VariableToObject
))
</jh>

== Singletons ==
Given a set (object) <code>s</code>, here we construct a set <code>{ s }</code> which has <code>s</code> as its only element. That is, we are trying to prove the theorem <code>y ∈ { s } ↔ y = s</code>.

Our first lemma is <code>y = s ↔ y ∈ ∅ ∨ y = s</code>, which is the first step towards applying the adjunction axiom to singletons. To avoid having to write <code>(value y)</code> everywhere, we use <code>yy</code> for an object which corresponds to the variable <code>y</code>.

Another detail is that if we specified the empty set as <code>(∅)</code>, it would expand to a dummy variable which we are unable to express a distinct variable constraint for.  By writing <code>{ emptyElement | ⊥ }</code> instead we get to include the distinct variable constraints on <code>emptyElement</code>.  In our informal descriptions we will continue to say <code>∅</code> in such situations.
<jh>
var (object yy)
thm (Singleton-1 ((yy emptyElement)) () ((yy = s) ↔ ((yy ∈ (abstract emptyElement (⊥))) ∨ (yy = s))) (
        (yy = s) (yy ∈ (abstract emptyElement (⊥))) DisjunctionLeftIntroduction

        yy emptyElement EmptySetObject
        (yy ≠ s) introduceAntecedent
        eliminateTransposition

        (yy = s) ImplicationReflexivity

        composeDisjunction

        introduceBiconditionalFromImplications
))
</jh>

In a few places we'll need <code>[y / z]z = s ↔ y = s</code>.
<jh>
thm (Singleton-eq ((yy z) (z s)) () ((subst yy z ((value z) = s)) ↔ (yy = s)) (
</jh>
Our substitution is <code>z = y → (z = s ↔ y = s)</code>.
<jh>
        ((value z) = yy) ImplicationReflexivity
        s buildEqualityRRInConsequent
</jh>
Making this explicit gives us our result.
<jh>
        makeSubstExplicit
))
</jh>

Applying <code>Singleton-eq</code> to the left hand side of <code>Singleton-1</code> gets us <code>[y / z]z = s ↔ y ∈ ∅ ∨ y = s</code>.
<jh>
thm (Singleton-2 ((yy z) (z s) (yy emptyElement)) () ((subst yy z ((value z) = s)) ↔ ((yy ∈ (abstract emptyElement (⊥))) ∨ (yy = s))) (
        yy z s Singleton-eq
        yy s emptyElement Singleton-1
        applyBiconditionalTransitivity
))
</jh>

The next step is to take <code>Singleton-2</code> and apply builders until we get <code>∃ result ∀ y (y ∈ result ↔ [y / z]z = s) ↔ ∃ result ∀ y (y ∈ result ↔ (y ∈ ∅ ∨ y = s))</code>
<jh>
thm (Singleton-3 ((y z) (z s) (y emptyElement)) () ((∃ result (∀ y (((value y) ∈ (value result)) ↔ (subst (value y) z ((value z) = s))))) ↔ (∃ result (∀ y (((value y) ∈ (value result)) ↔ (((value y) ∈ (abstract emptyElement (⊥))) ∨ ((value y) = s)))))) (
        (value y) z s emptyElement Singleton-2
        ((value y) ∈ (value result)) buildBiconditionalLL
        y buildForAll
        result buildThereExists
))
</jh>

The right hand side of <code>Singleton-3</code> holds by adjunction, so we can conclude the left hand side as a theorem.
<jh>
thm (Singleton-4 ((y z) (z s) (result y s) (result y emptyElement)) () (∃ result (∀ y (((value y) ∈ (value result)) ↔ (subst (value y) z ((value z) = s))))) (
        result y (abstract emptyElement (⊥)) s AdjunctionObject

        result y z s emptyElement Singleton-3
        eliminateBiconditionalForward

        applyModusPonens
))
</jh>

Because the notation <code>{ s }</code> is not available, we call it <code>(singleton s)</code>.
<jh>
def ((singleton s) (abstract y ((value y) = s)))
</jh>

We can now prove that <code>y ∈ { s } ↔ y = s</code>
<jh>
var (variable singletonElement)
thm (SingletonVariable ((result z y s)) () (((value y) ∈ (abstract z ((value z) = s))) ↔ ((value y) = s)) (
        result y z s Singleton-4
        Abstraction

        (value y) z s Singleton-eq
        applyBiconditionalTransitivity
))
</jh>

Getting rid of the distinct variable constraint and changing <code>y</code> from a variable to an object are accomplished by invoking <code>VariableToObject</code>.
<jh>
thm (SingletonObject ((y t) (y s singletonElement)) () ((t ∈ (abstract singletonElement ((value singletonElement) = s))) ↔ (t = s)) (
</jh>
Our substitution is <code>y = t → ((y ∈ { s } ↔ y = s) ↔ (t ∈ { s } ↔ t = s))</code>.
<jh>
        (value y) t (abstract singletonElement ((value singletonElement) = s)) IsElementBuilderRR
        (value y) t s EqualityBuilderRR
        buildBiconditionalInConsequent

        y singletonElement s SingletonVariable

        VariableToObject
))

thm (Singleton ((singletonElement s)) () ((t ∈ (singleton s)) ↔ (t = s)) (
        t singletonElement s SingletonObject
))
</jh>

=== Builder ===
The builder for singletons is <code>A = B → { A } = { B }</code>.<ref>[http://us.metamath.org/mpeuni/sneq.html sneq], metamath's set.mm, accessed July, 2010</ref>
<jh>
var (variable a b)
thm (SingletonBuilder-1 ((z A) (z B) (z a) (z b) (A a) (B b)) () ((A = B) → ((abstract a ((value a) = A)) = (abstract b ((value b) = B)))) (
</jh>
The equality builder gives us <code>A = B → (z = A ↔ z = B)</code>.
<jh>
        A B (value z) EqualityBuilderLL
</jh>
Applying <code>Singleton</code> twice gives us <code>(z ∈ {A} ↔ z ∈ {B}) ↔ (z = A ↔ z = B)</code>
<jh>
        (value z) a A SingletonObject
        (value z) b B SingletonObject
        buildBiconditional
</jh>
We now combine those two results to get <code>A = B → (z ∈ {A} ↔ z ∈ {B})</code>
<jh>
        eliminateBiconditionalForward
        applySyllogism
</jh>
Extensionality turns <code>z ∈ {A} ↔ z ∈ {B}</code> into <code>{A} = {B}</code>.
<jh>
        z addForAllToConsequent

        z (abstract a ((value a) = A)) (abstract b ((value b) = B)) ExtensionalityObject
        applySyllogism
))

thm (SingletonBuilder-2 ((A a) (B b)) () ((A = B) → ((singleton A) = (abstract b ((value b) = B)))) (
        A B a b SingletonBuilder-1
))

thm (SingletonBuilder ((B b)) () ((A = B) → ((singleton A) = (singleton B))) (
        A B b SingletonBuilder-2
))
</jh>

== Unordered pair ==

The unordered pair <code>{s, t}</code> has <code>s</code> and <code>t</code> as elements.  That is, we seek to prove <code>y ∈ {s, t} ↔ y = s ∨ y = t</code>. The idea of the proof is easy: apply adjunction to <code>{ s }</code> and <code>t</code>, but formalizing this involves much the same substitution machinery that we saw with singletons.

The first lemma is <code>y ∈ { s } ∨ y = t ↔ y = s ∨ y = t</code>.
<jh>
thm (UnorderedPair-1 ((singletonElement s)) () (((yy ∈ (abstract singletonElement ((value singletonElement) = s))) ∨ (yy = t)) ↔ ((yy = s) ∨ (yy = t))) (
        yy singletonElement s SingletonObject
        (yy = t) buildDisjunctionRR
))
</jh>

The next lemma, <code>[y / z](z = s ∨ z = t) ↔ y = s ∨ y = t</code>, is a substitution that we'll use two places.
<jh>
thm (UnorderedPair-sub ((z yy) (z s) (z t)) () ((subst yy z (((value z) = s) ∨ ((value z) = t))) ↔ ((yy = s) ∨ (yy = t))) (
</jh>
We construct the substitution <code>z = y → (z = s ∨ z = t ↔ y = s ∨ y = t)</code> and then make it explicit.
<jh>
        (value z) yy s EqualityBuilderRR
        (value z) yy t EqualityBuilderRR
        buildDisjunctionInConsequent

        makeSubstExplicit
))
</jh>

We now combine the previous two results and apply some builders to get <code>∃ result ∀ y (y ∈ result ↔ [y / z](z = s ∨ z = t)) ↔ ∃ result ∀ y (y ∈ result ↔ y ∈ { s } ∨ y = t)</code>
<jh>
thm (UnorderedPair-3 ((singletonElement s) (z y) (z s) (z t)) () ((∃ result (∀ y (((value y) ∈ (value result)) ↔ (subst (value y) z (((value z) = s) ∨ ((value z) = t)))))) ↔ (∃ result (∀ y (((value y) ∈ (value result)) ↔ (((value y) ∈ (abstract singletonElement ((value singletonElement) = s))) ∨ ((value y) = t)))))) (
        (value y) z s t UnorderedPair-sub
        (value y) singletonElement s t UnorderedPair-1 swapBiconditional
        applyBiconditionalTransitivity

        ((value y) ∈ (value result)) buildBiconditionalLL
        y buildForAll
        result buildThereExists
))
</jh>

The right hand side of <code>UnorderedPair-3</code> holds by adjunction, so we can conclude the left hand side as a theorem.
<jh>
thm (UnorderedPair-4 ((y z) (z s) (z t) (result y s) (result y t) (result y singletonElement) (singletonElement s)) ()
  (∃ result (∀ y (((value y) ∈ (value result)) ↔ (subst (value y) z (((value z) = s) ∨ ((value z) = t)))))) (
        result y (abstract singletonElement ((value singletonElement) = s)) t AdjunctionObject

        result y z s t singletonElement UnorderedPair-3
        eliminateBiconditionalForward

        applyModusPonens
))
</jh>

Because the notation <code>{ s, t }</code> is not available, we call it <code>(unorderedPair s t)</code>.
<jh>
def ((unorderedPair s t) (abstract y (((value y) = s) ∨ ((value y) = t))))
</jh>

We can now prove that <code>y ∈ { s, t } ↔ y = s ∨ y = t</code>
<jh>
var (variable pairElement)
thm (UnorderedPairVariable ((result z y s) (result z y t)) ()
  (((value y) ∈ (abstract z (((value z) = s) ∨ ((value z) = t)))) ↔ (((value y) = s) ∨ ((value y) = t))) (
        result y z s t UnorderedPair-4
        Abstraction

        (value y) z s t UnorderedPair-sub
        applyBiconditionalTransitivity
))
</jh>

Getting rid of the distinct variable constraint and changing <code>y</code> from a variable to an object are accomplished by invoking <code>VariableToObject</code>.
<jh>
thm (UnorderedPairObject ((y u) (y s pairElement) (y t pairElement)) () ((u ∈ (abstract pairElement (((value pairElement) = s) ∨ ((value pairElement) = t)))) ↔ ((u = s) ∨ (u = t))) (
</jh>
Our substitution is <code>y = u → ((y ∈ { s, t } ↔ y = s ∨ y = t) ↔ (u ∈ { s, t } ↔ u = s ∨ u = t))</code>.
<jh>
        (value y) u (abstract pairElement (((value pairElement) = s) ∨ ((value pairElement) = t))) IsElementBuilderRR

        (value y) u s EqualityBuilderRR
        (value y) u t EqualityBuilderRR
        buildDisjunctionInConsequent
        buildBiconditionalInConsequent

        y pairElement s t UnorderedPairVariable

        VariableToObject
))

thm (UnorderedPair ((pairElement s) (pairElement t)) () ((u ∈ (unorderedPair s t)) ↔ ((u = s) ∨ (u = t))) (
        u pairElement s t UnorderedPairObject
))
</jh>

=== Builder ===
The builder for unordered pairs is <code>A0 = B0 ∧ A1 = B1 → {A0, A1} = {B0, B1}</code>.
<jh>
thm (UnorderedPairBuilder-1
  ((z a) (z A0) (z A1) (z b) (z B0) (z B1) (a A0) (a A1) (b B0) (b B1))
  ()
  (((A0 = B0) ∧ (A1 = B1)) → ((abstract a (((value a) = A0) ∨ ((value a) = A1))) = (abstract b (((value b) = B0) ∨ ((value b) = B1))))) (
</jh>
Applying builders for equality and disjunction, we get <code>A0 = B0 ∧ A1 = B1 → (z = A0 ∨ z = A1 ↔ z = B0 ∨ z = B1)</code>.
<jh>
        (A0 = B0) (A1 = B1) ConjunctionRightElimination
        A0 B0 (value z) EqualityBuilderLL
        applySyllogism

        (A0 = B0) (A1 = B1) ConjunctionLeftElimination
        A1 B1 (value z) EqualityBuilderLL
        applySyllogism

        composeConjunction

        ((value z) = A0) ((value z) = B0) ((value z) A1) ((value z) A1) ((value z) B1) DisjunctionBuilder
        applySyllogism
</jh>
Applying <code>UnorderedPair</code>, we get <code>A0 = B0 ∧ A1 = B1 → (z ∈ {A0, A1} ↔ z ∈ {B0, B1})</code>.
<jh>
        (value z) a A0 A1 UnorderedPairObject
        swapBiconditional
        transformBiconditionalLeftInConsequent

        (value z) b B0 B1 UnorderedPairObject
        swapBiconditional
        transformBiconditionalRightInConsequent
</jh>
By extensionality, the right hand side implies <code>{A0, A1} = {B0, B1}</code>.
<jh>
        z addForAllToConsequent
        z (abstract a (((value a) = A0) ∨ ((value a) = A1))) (abstract b (((value b) = B0) ∨ ((value b) = B1))) ExtensionalityObject
        applySyllogism
))

thm (UnorderedPairBuilder-2 ((a A0) (a A1) (b B0) (b B1)) () (((A0 = B0) ∧ (A1 = B1)) → ((abstract a (((value a) = A0) ∨ ((value a) = A1))) = (unorderedPair B0 B1))) (
        A0 B0 A1 B1 a b UnorderedPairBuilder-1
))

thm (UnorderedPairBuilder ((a A0) (a A1)) () (((A0 = B0) ∧ (A1 = B1)) → ((unorderedPair A0 A1) = (unorderedPair B0 B1))) (
        A0 B0 A1 B1 a UnorderedPairBuilder-2
))
</jh>

== Done ==
We have now proved all the theorems in [[Interface:General set theory theorems]].

<jh>
export (GENERAL Interface:General_set_theory_theorems (CLASSICAL FIRSTORDER) ())
</jh>

== References ==

<references />

[[Category:Axiomatics of classical set theory and its fragments‎]]

