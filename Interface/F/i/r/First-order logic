{{header
 | title    = First-order logic
 | subtitle = 
 | left     = 
 | right    = 
 | shortcut = 
 | notes    = This interface collects important theorems from first-order logic, which builds on [[Interface:Classical propositional calculus|propositional logic]] by adding quantifiers ("for all" and "there exists") and equality. It is meant as a user-friendly reservoir for those interfaces and proof modules which merely use first-order logic, without proving anything new within it. It does not include results which rely on the [[Interface:Axiom of quantifiability]]; many modules may wish to import [[Interface:First-order logic with quantifiability]] instead of this interface.

Statements can be added if necessary, but then the exporting modules have to be updated accordingly.
}}
{{modules
| parameters = [[Interface:Classical propositional calculus]]
| importedby = [[Interface:First-order logic with quantifiability]]
| exportedby = [[First-order logic]]
}}
== Formulas, objects, and quantification ==
We build on [[Interface:Classical propositional calculus]]:
<jh>
param (CLASSICAL Interface:Classical_propositional_calculus () ())
</jh>

By convention we use φ, ψ, χ, and θ to represent formulas:
<jh>
var (formula φ ψ χ θ)
</jh>

We use the term ''object'' to refer to something which is subject to quantification or equality. From an axiomatic point of view, what is a valid object is defined implicitly, by axioms such as those of [[w:Zermelo–Fraenkel set theory|ZFC set theory]] or [[w:Peano axioms|Peano arithmetic]].

<jh>
kind (object)
var (object s t u s0 s1 t0 t1)
</jh>

A ''variable'' is something we can quantify over.

<jh>
kind (variable)
var (variable x y z x0 x1 y0 y1)
</jh>

A variable can be used where an object is expected, although it needs to be converted via the following explicit conversion.  When we are writing informally (rather than in JHilbert proofs themselves), we may omit the conversion (it should be understood whereever we use a variable in a context where an object is expected).
<jh>
term (object (value variable))
</jh>

The reverse is not possible.  Examples of objects which are not variables might be <code>s + t</code> for Peano arithmetic, or <code>s ∪ t</code> for set theory.

We extend the available formulas with [[w:Universal quantification|universal quantification]] and [[w:Existential quantification|existential quantification]]:
<jh>
term (formula (∀ variable formula))
term (formula (∃ variable formula))
</jh>

== Relationship between universal and existential quantification ==

<jh>
stmt (NotForAll () () ((¬ (∀ x φ)) ↔ (∃ x (¬ φ))))
stmt (NotThereExists () () ((¬ (∃ x φ)) ↔ (∀ x (¬ φ))))
</jh>

== Variations and consequences of double quantification elimination ==
<jh>
stmt (ax6o () () ((∃ x (∀ x φ)) → φ))
stmt (a5i () (((∀ x φ) → ψ)) ((∀ x φ) → (∀ x ψ)))
</jh>

== Builders for quantifiers ==
As with propositional calculus, we need to build up formulas explicitly, and these rules let us do it.  We provide them for both quantifiers, for implication and the biconditional.
<jh>
stmt (addForAll () ((φ → ψ)) ((∀ x φ) → (∀ x ψ)))
stmt (addThereExists () ((φ → ψ)) ((∃ x φ) → (∃ x ψ)))

stmt (buildForAll () ((φ ↔ ψ)) ((∀ x φ) ↔ (∀ x ψ)))
stmt (buildThereExists () ((φ ↔ ψ)) ((∃ x φ) ↔ (∃ x ψ)))
</jh>

=== Additional builders ===
Some of these builders rely on theorems we haven't presented yet, but they are similar to the simple ones above in terms of general intent.
<jh>
stmt (buildThereExistsInConsequent ((x φ)) 
  ((φ → (ψ ↔ χ)))
  (φ → ((∃ x ψ) ↔ (∃ x χ))))
stmt (buildForAllInConsequent ((x φ))
  ((φ → (ψ ↔ χ)))
  (φ → ((∀ x ψ) ↔ (∀ x χ))))
</jh>

== Order ==
The order of adjacent quantifiers of the same type can be exchanged:
<jh>
stmt (ForAllCommutation () () ((∀ x (∀ y φ)) ↔ (∀ y (∀ x φ))))
stmt (ThereExistsCommutation () () ((∃ x (∃ y φ)) ↔ (∃ y (∃ x φ))))
</jh>

∃ followed by ∀ can be switched:
<jh>
stmt (ThereExistsForAllCommutation () () ((∃ x (∀ y φ)) → (∀ y (∃ x φ))))
</jh>

but the converse is only possible in special cases:
<jh>
stmt (ForAllThereExistsImplicationCommutation ((x y) (ψ x) (φ y)) ()
  ((∃ x (∀ y (φ → ψ))) ↔ (∀ y (∃ x (φ → ψ))))
)
</jh>

== Combining quantified statements ==
Quantifiers of the same variable can sometimes be combined or split:
<jh>
stmt (ForAllConjunction () () ((∀ x (φ ∧ ψ)) ↔ ((∀ x φ) ∧ (∀ x ψ))))
stmt (ThereExistsDisjunction () () ((∃ x (φ ∨ ψ)) ↔ ((∃ x φ) ∨ (∃ x ψ))))
stmt (ThereExistsImplication () () ((∃ x (φ → ψ)) ↔ ((∀ x φ) → (∃ x ψ))))
stmt (ForAllImplication () () ((∀ x (φ → ψ)) → ((∀ x φ) → (∀ x ψ))))
</jh>

We also provide rules for the above theorems:
<jh>
stmt (distributeForAllConjunction () ((∀ x (φ ∧ ψ))) ((∀ x φ) ∧ (∀ x ψ)))
stmt (collectForAllConjunction () ((∀ x φ) (∀ x ψ)) (∀ x (φ ∧ ψ)))

stmt (distributeThereExistsDisjunction () ((∃ x (φ ∨ ψ)))  ((∃ x φ) ∨ (∃ x ψ)))
stmt (collectThereExistsDisjunction () (((∃ x φ) ∨ (∃ x ψ))) (∃ x (φ ∨ ψ)))

stmt (distributeThereExistsImplication () ((∃ x (φ → ψ))) ((∀ x φ) → (∃ x ψ)))
stmt (collectThereExistsImplication () (((∀ x φ) → (∃ x ψ))) (∃ x (φ → ψ)))

stmt (distributeForAllImplication () ((∀ x (φ → ψ))) ((∀ x φ) → (∀ x ψ)))
</jh>

=== Biconditional ===
<jh>
stmt (ForAllBiconditional () () ((∀ x (φ ↔ ψ)) → ((∀ x φ) ↔ (∀ x ψ))))
</jh>

=== Other kinds of splits and combinations ===
<jh>
stmt (ForAllImplicationThereExists () () ((∀ x (φ → ψ)) → ((∃ x φ) → (∃ x ψ))))
stmt (ForAllBiconditionalThereExists () () ((∀ x (φ ↔ ψ)) → ((∃ x φ) ↔ (∃ x ψ))))
stmt (ThereExistsConjunctionCombining () () (((∀ x φ) ∧ (∃ x ψ)) → (∃ x (φ ∧ ψ))))
</jh>

== Generalization and specialization ==
We can remove a universal quantifier:
<jh>
stmt (Specialization () () ((∀ x φ) → φ))
stmt (specialize () ((∀ x φ)) φ)
</jh>

The converse is sometimes possible, but only with some restrictions. Either the variable does not occur in the formula, or the formula is unconditionally true:
<jh>
stmt (Generalization ((x φ)) () (φ → (∀ x φ)))
stmt (generalize () (φ) (∀ x φ))
</jh>

An existential quantifier can be removed if the variable does not exist in the formula.
<jh>
stmt (NullThereExists ((x φ)) () ((∃ x φ) ↔ φ))
</jh>

== Equality ==
Equality is reflexive, symmetry, and transitive.
<jh>
term (formula (= object object))
stmt (EqualityReflexivity () () (s = s))

stmt (EqualitySymmetry () () ((s = t) ↔ (t = s)))
stmt (swapEquality () ((s = t)) (t = s))

stmt (EqualityTransitivity () () (((s = t) ∧ (t = u)) → (s = u)))
stmt (applyEqualityTransitivity () ((s = t) (t = u)) (s = u))
</jh>

There is also the principle of "equals can be substituted for equals". In our system, this is done via builders, and here we provide one for equals itself. Theories which build on first-order logic will provide builders for additional expressions such as <code>∈</code> (is an element of) for set theory or <code>+</code> (addition) for Peano arithmetic.
<jh>
stmt (EqualityBuilder () () (((s0 = s1) ∧ (t0 = t1)) → ((s0 = t0) ↔ (s1 = t1))))
stmt (buildEquality () ((s0 = s1) (t0 = t1)) ((s0 = t0) ↔ (s1 = t1)))
</jh>

We also supply a variety of variant builders, for convenience:
<jh>
stmt (EqualityBuilderRR () () ((s0 = s1) → ((s0 = t) ↔ (s1 = t))))
stmt (buildEqualityRR () ((s0 = s1)) ((s0 = t) ↔ (s1 = t)))

stmt (EqualityBuilderLL () () ((s0 = s1) → ((t = s0) ↔ (t = s1))))
stmt (buildEqualityLL () ((t0 = t1)) ((s = t0) ↔ (s = t1)))

stmt (buildEqualityInConsequent () ((φ → (s0 = s1)) (φ → (t0 = t1))) (φ → ((s0 = t0) ↔ (s1 = t1))))
stmt (buildEqualityRRInConsequent () ((φ → (s0 = s1))) (φ → ((s0 = t) ↔ (s1 = t))))
</jh>

We also define <code>≠</code>, which is just an abbreviation for negation and equality.

<jh>
def ((≠ s t) (¬ (s = t)))
</jh>

== Free variables ==
The conventional treatment of [[w:free variable|free variables]] is syntactic; variables are defined as free depending on where they occur in a formula relative to the quantifiers over that variable. Our concept of free variable is similar (and can be used in applying first-order logic in exactly the same way), although we do provide one additional way to consider a variable "free" which would not be present in most systems.

<jh>
term (formula (is-not-free-in variable formula))
</jh>

=== Showing a variable is (effectively) free ===
The key way that our concept of free variable differs from the syntactic one is that we consider <code>is-not-free-in</code> to be true for any theorem (that is, unconditionally true statement), even if the variable occurs in the theorem without a quantifier. There is probably little need to use the next statement (since most treatments of first-order logic do not have it); it is here to underscore that it would not be possible to prove a statement such as <code>(¬ (x is-not-free-in (x = x)))</code>.
<jh>
stmt (TheoremNotFree () (φ) (x is-not-free-in φ))
</jh>

=== Showing a variable is free ===
Using the syntactic definition of free variable, the equivalent of <code>is-not-free-in</code> would be true for all the reasons described in this section.

A variable is not free in a formula if it does not occur in the formula:
<jh>
stmt (DistinctNotFree ((x φ)) () (x is-not-free-in φ))
</jh>

A variable is not free in a formula if it is bound by a quantifier:
<jh>
stmt (BoundForAllNotFree () () (x is-not-free-in (∀ x φ)))
stmt (BoundThereExistsNotFree () () (x is-not-free-in (∃ x φ)))
</jh>

If a variable is not free in a formula it is not free in a formula which adds another quantifier (possibly to a different variable) at the start:
<jh>
stmt (addForAllNotFree () ((x is-not-free-in φ)) (x is-not-free-in (∀ y φ)))
stmt (addThereExistsNotFree () ((x is-not-free-in φ)) (x is-not-free-in (∃ y φ)))
</jh>

If a variable is not free in any of the parts of a formula it is not free in the formula which joins them with connectives:

<jh>
stmt (negateNotFree () ((x is-not-free-in φ)) (x is-not-free-in (¬ φ)))

stmt (implicationNotFree () 
  ((x is-not-free-in φ) (x is-not-free-in ψ))
  (x is-not-free-in (φ → ψ))
)

stmt (disjunctionNotFree () 
  ((x is-not-free-in φ) (x is-not-free-in ψ))
  (x is-not-free-in (φ ∨ ψ)))

stmt (conjunctionNotFree ()
  ((x is-not-free-in φ) (x is-not-free-in ψ))
  (x is-not-free-in (φ ∧ ψ)))

stmt (biconditionalNotFree ()
  ((x is-not-free-in φ) (x is-not-free-in ψ))
  (x is-not-free-in (φ ↔ ψ)))
</jh>

And we supply a builder for <code>is-not-free-in</code>:

<jh>
stmt (buildNotFree () ((φ ↔ ψ)) ((x is-not-free-in φ) ↔ (x is-not-free-in ψ)))
</jh>

=== Adding or removing quantifiers ===
If a variable is not free in a formula, it is legitimate to add or remove quantifiers from that formula:
<jh>
stmt (ForAllAddRemove () ((x is-not-free-in φ)) ((∀ x φ) ↔ φ))
stmt (ThereExistsAddRemove () ((x is-not-free-in φ)) ((∃ x φ) ↔ φ))
</jh>

=== Moving formulas in and out of quantification ===
The results in this section all have to do with being able to move around formulas relative to quantifiers, given that the quantified variable is not free in the formula being moved.

==== Implication ====
<jh>
stmt (ForAllImplicationAntecedentMovement () ((x is-not-free-in φ)) ((∀ x (φ → ψ)) ↔ (φ → (∀ x ψ))))
stmt (moveAntecedentOutOfForAll () ((∀ x (φ → ψ)) (x is-not-free-in φ)) (φ → (∀ x ψ)))
stmt (moveAntecedentIntoForAll () 
  ((φ → (∀ x ψ)) (x is-not-free-in φ)) 
  (∀ x (φ → ψ)))

stmt (ThereExistsImplicationAntecedentMovement () ((x is-not-free-in φ)) ((∃ x (φ → ψ)) ↔ (φ → (∃ x ψ))))
stmt (ForAllImplicationConsequentMovement () ((x is-not-free-in ψ)) ((∀ x (φ → ψ)) ↔ ((∃ x φ) → ψ)))
stmt (ThereExistsImplicationConsequentMovement () ((x is-not-free-in ψ)) ((∃ x (φ → ψ)) ↔ ((∀ x φ) → ψ)))

stmt (ThereExistsConjunctionMovementNotFree () ((x is-not-free-in φ)) ((∃ x (φ ∧ ψ)) ↔ (φ ∧ (∃ x ψ))))
</jh>

==== Versions with distinct variables ====
<jh>
stmt (ThereExistsConjunctionMovement ((x φ)) () ((∃ x (φ ∧ ψ)) ↔ (φ ∧ (∃ x ψ))))
stmt (ThereExistsConjunctionRightMovement ((x ψ)) () ((∃ x (φ ∧ ψ)) ↔ ((∃ x φ) ∧ ψ)))
</jh>

==== Scattering and gathering ====
If we have two formulas joined by a conjunction, one quantified over one variable and the other quantified over another, we can express the quantifiers either at the beginning or with the formulas they apply to.<ref>[http://us.metamath.org/mpeuni/eeanv.html eeanv], metamath's set.mm, accessed August 8, 2010</ref> We name the propositions <code>φx</code> and <code>ψy</code> to express the idea that <code>φx</code> will typically contain <code>x</code> and <code>ψy</code> will typically contain <code>y</code>.
<jh>
var (formula φx ψy)
stmt (ThereExistsScattering ((φx y) (ψy x)) ()
  ((∃ x (∃ y (φx ∧ ψy))) ↔ ((∃ x φx) ∧ (∃ y ψy))))
</jh>

==== Adding quantifiers to theorems which are implications ====

These variants on moving formulas turn out to be used particularly often:
<jh>
stmt (addThereExistsToAntecedent ((x ψ))
  ((φ → ψ))
  ((∃ x φ) → ψ))
stmt (addForAllToConsequent ((x φ))
  ((φ → ψ))
  (φ → (∀ x ψ)))

stmt (addForAllToConsequentNotFree () ((x is-not-free-in φ) (φ → ψ)) (φ → (∀ x ψ)))
</jh>

=== Other theorems related to substitution ===
<jh>
stmt (equs3 () () 
  ((∃ x (((value x) = s) ∧ φ)) ↔ (¬ (∀ x (((value x) = s) → (¬ φ))))))
stmt (VariableSubstitution ((x y)) () 
  (((value x) = (value y)) → (φ → (∀ x (((value x) = (value y)) → φ)))) )
</jh>

== Propositional logic ==
These surely belong elsewhere, but are here for the moment.

<jh>
stmt (joinConsequents ()
  ((φ → ψ) (φ → χ))
  (φ → (¬ (ψ → (¬ χ)))))
</jh>

== What else? ==
This page is not complete.  If you know what should be here, edit the page or suggest it on the talk page.

[[Category:Classical first-order logic|{{PAGENAME}}]]