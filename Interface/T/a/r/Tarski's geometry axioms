{{header
 | title    = Tarski's axioms
 | subtitle = 
 | left     = 
 | right    = 
 | shortcut = 
 | notes    = Since [[w:Euclid|Euclid]], geometry has been presented as following from postulates. However, it was not until the 20th century that a complete set of postulates were put forward. This page is for one such set, the [[w:Tarski's axioms|first-order axiomization by Tarski]]. It is less powerful than full Euclidian geometry and more powerful than compass-and-straightedge geometry.<ref>footnote 5 on page 191 of Tarski and Givant (1999)</ref> It makes reference to points (not directly to lines, angles, or circles), and has two relationships between points: betweenness and congruence. The version here is for two dimensions. Adapting the axioms for one dimension is complicated,<ref>Tarski and Givant (1999), pages 204–209</ref> but only slight modifications are needed for more than two dimensions.<ref>Tarski and Givant (1999), page 195</ref>
}}
{{modules
| parameters = [[Interface:Classical propositional calculus|Classical propositional calculus]], [[Interface:First-order logic with quantifiability]]
| importedby = [[Line segment congruence]]
| exportedby = [[Tarski's geometry axioms derived from real numbers]]
}}

First, we adopt the axioms of [[Interface:Classical propositional calculus|propositional logic]] and [[Interface:First-order logic with quantifiability|first-order logic]] (including equality):

<jh>
param (CLASSICAL Interface:Classical_propositional_calculus () ())
param (FIRSTORDER Interface:First-order_logic_with_quantifiability (CLASSICAL) ())
</jh>

The kind <code>object</code>, defined in first-order logic, represents a point:
<jh>
kindbind (object point)
var (point x y z w u v)

var (variable a b c xx yy)
</jh>

== Congruence ==
We introduce congruence of line segments; <code>(x y ≡ w z)</code> means that the line segment xy is the same length as the line segment wz. This property is also known as equidistance.<ref>Tarski and Givant, 1999, page 177</ref>
<jh>
term (formula (≡ point point point point))
</jh>

The distance from x to y is the same as the distance from y to x.  This property is called reflexivity by Tarski and Givant<ref>Tarski and Givant, 1999, page 177</ref> and symmetry by Narboux.<ref>Narboux, 2007, page 141</ref> Narboux also calls it the pseudo-commutativity property of the oriented distance (on page 148) and in his proofs pseudo_reflexivity.<ref>Narboux, 2007</ref>
<jh>
stmt (CongruenceABBA () () (x y ≡ y x))
</jh>

A segment which has zero length starts and ends at the same point (although saying "zero length" is a bit of a shortcut, as the axioms are not based on real numbers or any other numbers).
<jh>
stmt (CongruenceIdentity () () ((x y ≡ z z) → (x = y)))
</jh>

Two segments which are congruent to a common segment are congruent to each other.
<jh>
stmt (CongruenceTransitivityAxiom () () (((x y ≡ z u) ∧ (x y ≡ v w)) → (z u ≡ v w)))
</jh>

== Betweenness ==
The other fundamental formula is betweenness; <code>(between x y z)</code> signifies "y is between x and z".

<jh>
term (formula (between point point point))
</jh>

There are no other points between x and x.
<jh>
stmt (Indivisibility () () ((between x y x) → (x = y)))
</jh>

=== Pasch's axiom ===
[[File:Tarski's formulation of Pasch's axiom.svg|left|thumb]]
Tarski's version of the [[w:Axiom of Pasch]].
{{clear}}
<jh>
stmt (Pasch ((a x) (a u) (a z) (a y) (a v)) () (((between x u z) ∧ (between y v z)) → (∃ a ((between u (value a) y) ∧ (between v (value a) x)))))
</jh>

=== Continuity ===
[[File:Tarski's continuity axiom.svg|left|thumb]]
The variables <code>xx</code> and <code>yy</code> correspond with <code>x</code> and <code>y</code> in the diagram and [[w:Tarski's axioms|wikipedia]]. (We can't just call them <code>x</code> and <code>y</code> because we've declared <code>x</code> and <code>y</code> as objects, which unlike variables cannot be subject to quantification).
<jh>
var (formula φx ψy)
stmt (Continuity ((φx yy) (φx a) (φx b) (ψy xx) (ψy a) (ψy b) (a b xx yy)) () 
  ((∃ a (∀ xx (∀ yy ((φx ∧ ψy) → (between (value a) (value xx) (value yy))))))
  → (∃ b (∀ xx (∀ yy ((φx ∧ ψy) → (between (value xx) (value b) (value yy))))))))
</jh>

== Dimension ==
[[File:Points in a plane equidistant to two given points lie on a line.svg|thumb|right|Upper dimension]]
The dimension is greater than one,
<jh>
stmt (LowerDimension ((a b c)) ()
  (∃ a (∃ b (∃ c
    (((¬ (between (value a) (value b) (value c)))
     ∧ (¬ (between (value b) (value c) (value a))))
     ∧ (¬ (between (value c) (value a) (value b)))
)))))
</jh>

and less than three.
<jh>
stmt (UpperDimension () ()
  (((((x u ≡ x v) ∧ (y u ≡ y v)) ∧ (z u ≡ z v)) ∧ (u ≠ v))
  → (((between x y z) ∨ (between y z x)) ∨ (between z x y))))
</jh>

== Axiom of Euclid ==
[[File:Tarski's axiom of Euclid C.svg|thumb|right]]
There are quite a variety of ways to state Euclid's [[w:parallel postulate]]. Here we adopt one which says that if a point u is in the interior of ∠yxz (in the sense of being on the line segment yz drawn across the angle), then any point v which is further out (that is, u is between v and the vertex of the angle) will also be in the interior (in the sense that a line segment containing v can be drawn across the angle).
<jh>
stmt (AxiomEuclid ((a b x) (a b u) (a b v) (a b y) (a b z)) () (
  (((between x u v) ∧ (between y u z)) ∧ (x ≠ u))
  → (∃ a (∃ b (((between x y (value a)) ∧ (between x z (value b))) ∧ (between (value a) v (value b)))))))
</jh>

== Five Segment ==
[[File:Five segment.svg|thumb|right]]
This is a version of familiar theorems concerning congruent triangles (without any explicit reference to angles, of course).
<jh>
var (point x′ y′ z′ u′)
stmt (OuterFiveSegment () ()
  ((((((((x ≠ y) ∧ (between x y z)) ∧ (between x′ y′ z′))
     ∧ (x y ≡ x′ y′)) ∧ (y z ≡ y′ z′)) ∧ (x u ≡ x′ u′)) ∧ (y u ≡ y′ u′))
   → (z u ≡ z′ u′))
)
</jh>

== Segment Construction ==
Given a line segment wx and a line segment yz, construct an extension of wx which is as long as yz is.
<jh>
stmt (SegmentConstruction ((a w) (a x) (a y) (a z)) () (∃ a ((between w x (value a)) ∧ (x (value a) ≡ y z))))
</jh>

== Builders ==
Being able to substitute equals for equals is generally taken as a logical axiom, but we need to provide it for every operator.
<jh>
var (point x0 x1 x2 x3 y0 y1 y2 y3)
stmt (CongruenceBuilder () () (
  ((((x0 = y0) ∧ (x1 = y1)) ∧ (x2 = y2)) ∧ (x3 = y3))
  → ((x0 x1 ≡ x2 x3) ↔ (y0 y1 ≡ y2 y3))
))
stmt (BetweenBuilder () () (
  (((x0 = y0) ∧ (x1 = y1)) ∧ (x2 = y2))
  → ((between x0 x1 x2) ↔ (between y0 y1 y2))
))
</jh>

== Comments about the predicate logic ==

There are two related comments to make about translating the predicate logic used in Tarski and Givant to what we use here. The Tarski and Givant paper, except as explicitly noted in axiom schema As. 11, does not contain axiom schemas but instead concrete axioms. The corresponding concept, given our [[Interface:First-order logic with quantifiability|first-order logic]], would be to use <code>variable</code> (rather than <code>object</code>) everywhere with distinct variable constraints between all variables (again, except for As. 11 which has explicit distinct variable constraints in the Tarski and Givant paper).<ref>Tarski and Givant, 1999, page 177 and page 185</ref>
For convenience, we loosen this in two ways. First of all, we use <code>object</code> instead of <code>variable</code> for all variables not subject to quantification (for comparison, this is similar to the way that metamath handles their complex number axioms<ref>[http://us.metamath.org/mpeuni/mmcomplex.html Real and Complex Numbers], last updated on May 6, 2008, paragraph beginning "In case you are wondering: Why do we use the purple class variables for most postulates instead of the more conventional-looking red set variables?"</ref>). Secondly, we only supply distinct variable constraints where quantifiers are involved (this also parallels metamath, although the metamath page doesn't explicitly discuss it). These changes do not affect the mathematical meaning of the axioms or their strength: for an example of translating axioms stated using <code>variable</code> and distinct variable constraints on everything, to axioms in the style presented here, see [[First steps in set theory]].

== Tarski's axioms as the basis for geometry ==
Tarski's axioms are intended to be sufficient for the development of classical geometry (in the style of Euclid's ''Elements'' and similar works). Getting from the axioms to the familiar theorems involving congruent triangles, angles, midpoints, perpendicular and parallel lines, and the like does entail quite a few proofs. They are broken up into the following pages, with references to the corresponding chapters in Narboux.<ref>Narboux, 2007, which references the chapter numbering to W. Schwabhäuser, W Szmielew, and A. Tarski (1983), ''Metamathematische Methoden in der Geometrie'', ISBN 0387129588</ref>
*[[Line segment congruence]]. Also includes outer three segment and segment construction uniqueness. (chapter 2)
*[[Betweenness of points]], including the existence of distinct points, <code>PaschLine</code> (chapter 3)
*[[Triangle congruence]] defined and with some basic results for degenerate triangles.  Also includes inner five segment. (first half of chapter 4)
*[[Collinearity]] (second half of chapter 4)
*[[Connectivity for betweenness]] (first half of chapter 5)
*[[Line segment inequality]] including an additional line segment construction theorem (second half of chapter 5)
*[[Out lines]] (chapter 6)
*[[Symmetric point]] (first portion of chapter 7)
*[[Midpoint]] (latter portion of chapter 7)
*[[Orthogonality]] (chapter 8)
*[[The plane]] (chapter 9)
*[[Line reflexivity]] (chapter 10)
*[[Angles]] (chapter 11)
*[[Parallelism]] (chapter 12)

== References ==
<references/>
* [[w:Tarski's axioms]]
* Tarski, Alfred; Givant, Steven (1999), "Tarski's system of geometry", The Bulletin of Symbolic Logic 5 (2): 175–214, [[doi:10.2307/421089]], MR1791303, ISSN 1079-8986
* Julien Narboux (2007), "[http://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.158.8614 Mechanical Theorem Proving in Tarski’s Geometry]", F. Botana and T. Recio (Eds.): ADG 2006, LNAI 4869, pp. 139–156

{{DEFAULTSORT:{{PAGENAME}}}}
[[Category:Euclidean geometries (general) and generalizations]]
[[Category:Foundations of classical theories (including reverse mathematics)]]